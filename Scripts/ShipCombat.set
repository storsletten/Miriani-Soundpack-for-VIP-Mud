#Var TotalHits 0
#Var TotalHitsOnThisShip 0
#Var DirectHits 0
#Var PartialHits 0
#Var DestroyedShips 0
#Var TotalShips 0
#Var DestroyedDebris 0

#Var ShipClasses {
Bzani:-70
Ontanka:-40
Otono:-25
Otona:-25
Muzati:-15
Orta:-15
Onati:10
Muzano:20
Potate:20
Ohaxx:20
}

#Alias {HitCounter} {
 #If {%1="clear"} {
  #Var DirectHits 0;
  #Var PartialHits 0;
  #Var DestroyedShips 0;
  #Var TotalHits 0;
  #Var TotalHitsOnThisShip 0;
  #Var TotalShips 0;
  #Var DestroyedDebris 0;
  #If {%2<>1} {ConfPrint {Hit counter cleared.}}
 } {
  #Var ShipCombatTmp1 {Total hits: %Eval(@DirectHits+@PartialHits), of which @DirectHits were direct and @PartialHits were partial.
You have destroyed @DestroyedShips ships of @TotalShips ships, and @DestroyedDebris debris.
You have been hit @TotalHitsOnThisShip times.
Use HitCounter clear to clear these stats.};
  #If {@UseHitCounter=0} {#Var ShipCombatTmp1 {@ShipCombatTmp1
Note: You have use hit counter disabled. You can enable this in Conf Ship.}
  };
  ConfPrint {@ShipCombatTmp1}
 }
}

#Trigger {Locked onto coordinates: *} {
 #GagLine 1 Voice;
 #Sub {Locked on %1};
 #If {@UseAlternativeCombatSounds=1 or %1=@FocusShipCoords} {MirianiSoundPlay Standard Ship\Combat\LockedOnOther2@Ext}
} {Main|Starship}
#Trigger {A blinding flash of light suddenly permeates the *.} {
 MirianiSoundPlay Standard Ship\Combat\YouHit*9@Ext;
 #GagLine 1 All
} {Main|Starship}
#Trigger {Target: *} {
 MirianiSoundPlay Standard Ship\Combat\LockedOnOther@Ext;
 #If {%Pos(Starship ~",%1)=1} {
  #If {@MissionMode=1 and %Pos(Starship ~"Praelor ,%1)=1} {#GagLine 1 Voice};
  #Sub {Target %Copy(%1,11,%Length(%1)-11).}
 } {
  #Sub {%1 targeted.}
 }
} {Main|Starship}
#Trigger {* has a lock on this starship.} {
 #If {@EnhancedAimSoundSystem>0} {
  #If {%IfWord(%1,@FocusShipName,%CRLF)=1} {
   MirianiSoundPlay Standard Ship\Combat\LockedOnYou2@Ext 15;
   #PC %PlayHandle Frequency 66000
  } {
   #If {@EnhancedAimSoundSystem<2} {
    MirianiSoundPlay Standard Ship\Combat\LockedOnYou2@Ext -10;
    #PC %PlayHandle Frequency 44100
   }
  }
 } {
  MirianiSoundPlay Standard Ship\Combat\LockedOnYou@Ext -10;
  #PC %PlayHandle Frequency 44100
 };
 #If {@MissionMode=1 and %Pos(Praelor ,%1)=1} {#GagLine 1 Voice};
 #Sub {Lock by %1.}
} {Main|Starship}
#Trigger {* has fired at *.} {
 #If {%Pos(~(,%1)=1 and %Pos(~),%1)>1} {} {
  #Var DisplayString {%2 hit by %1.};
  #If {%Word(%2, ,1)="Praelor"} {
   #If {@MissionMode=1} {#GagLine 1 Voice}
  } {
   PushMessage {Intrasector0Combat} {@DisplayString}
  };
  DisplayPrint 4 {@DisplayString} {%0};
  mwplay {Ship\Combat\OtherFired*6@Ext} {0:4};
  #Sub {@DisplayString}
 }
} {Main}
#Trigger {A * as * lights * overhead.} {
 #If {%3=*"continue to *"} {
  #If {%2="red" and @MuteAlertContSounds<>1} {
   #If {%mp3loop=0} {mtplay};
   MirianiSoundPlay Standard Ship\Combat\RedCont*3@Ext;
   #If {@SoundIsPlaying=1} {#Var AlertSoundHandle {%PlayHandle}} {#UnVar AlertSoundHandle}
  };
  #If {%2="orange" and @MuteAlertContSounds<>1} {
   MirianiSoundPlay Standard Ship\Combat\OrangeCont@Ext;
   #If {@SoundIsPlaying=1} {#Var AlertSoundHandle {%PlayHandle}} {#UnVar AlertSoundHandle}
  };
  #If {%2="purple" and @MuteAlertContSounds<>1} {
   MirianiSoundPlay Standard Ship\Combat\PurpleCont@Ext;
   #If {@SoundIsPlaying=1} {#Var AlertSoundHandle {%PlayHandle}} {#UnVar AlertSoundHandle}
  };
  #If {%2="blue"} {
   MirianiSoundPlay Standard Ship\Computer\SelfDestructStart@ext;
   #If {@SoundIsPlaying=1} {#Var AlertSoundHandle {%PlayHandle}} {#UnVar AlertSoundHandle}
  };
  #If {%IfWord(%2,red blue orange purple, )>0} {
   #GagLine 1 All
  }
 } {
  #If {%1="loud rendition of Jingle Bells sounds"} {
   #GagLine 1 All
  } {
   #If {@WakeOnRed=1 and %IfWord(%2,blue orange purple red, )=1} {
    #If {@GagEverything=1} {GagEverything};
    #If {@Silent=1} {dnd off}
   };
   #If {%2="red"} {
    MirianiSoundPlay Standard Ship\Combat\RedStart*2@Ext;
    #If {@SoundIsPlaying=1} {#Var AlertSoundHandle {%PlayHandle}} {#UnVar AlertSoundHandle}
   };
   #If {%2="blue"} {
    MirianiSoundPlay Standard Ship\Computer\SelfDestructStart@ext;
    #If {@SoundIsPlaying=1} {#Var AlertSoundHandle {%PlayHandle}} {#UnVar AlertSoundHandle}
   };
   #If {%2="purple"} {
    MirianiSoundPlay Standard Ship\Combat\PurpleAlert@Ext;
    #If {@SoundIsPlaying=1} {#Var AlertSoundHandle {%PlayHandle}} {#UnVar AlertSoundHandle}
   };
   #Sub {%Proper(%2) alert initiated.};
   #GagLine 1 Voice
  }
 }
} {Main|Starship}
#Trigger {This starship has been hit by *.} {
 #Math TotalHitsOnThisShip {@TotalHitsOnThisShip+1};
 #If {@EnhancedAimSoundSystem>0 and %IfWord(%1,@FocusShipName,%CRLF)=1} {MirianiSoundPlay Standard Ship\Computer\SensorInterference@Ext};
 #Var ShipCombatTmp1 0;
 #If {%Word(%1, ,1)="Praelor"} {
  #If {@MissionMode=1} {#GagLine 1 Voice};
  #If {%NumWords(%1, )>1 and @ClassDependant=1} {
   #Var ShipCombatTmp1 {%CRLF%Word(%1, ,2):};
   #Var ShipCombatTmp1 {%Pos(@ShipCombatTmp1,@ShipClasses)};
   #If {@ShipCombatTmp1>0} {
    #Var ShipCombatTmp1 {%NumWords(%Copy(@ShipClasses,1,@ShipCombatTmp1+2),@LF)};
    #Var ShipCombatTmp1 {%Word(@ShipClasses,%CRLF,@ShipCombatTmp1)};
    #Var ShipCombatTmp1 {%Word(@ShipCombatTmp1,:,2)}
   } {#Var ShipCombatTmp1 0}
  }
 };
 mwplay Ship\Combat\YouHit*11@Ext @ShipCombatTmp1;
 #Sub {Hit by %1.}
} {Main|Starship}
#Trigger {The room is flooded with a brilliant * light as lasers from * impact the hull.} {
 mwplay Ship\Combat\YouHit*11@Ext;
 #Sub {Hit by %2's %1 laser fire.}
} {Main|Starship}
#Trigger {The ship rocks slightly as it's hit by *} {
 mwplay Ship\Combat\YouHit*11@Ext;
 #Sub {Hit by %1}
} {Main|Starship}
#Trigger {The room is filled with * as the laser turrets are fired.} {
 mwplay Ship\Combat\Laser*17@Ext;
 #GagLine 1 All
} {Main|Starship}
#Trigger {* has launched an interdictor.} {MirianiSoundPlay Standard ship\misc\intradictor@Ext} {Main}
#Trigger {*A distant whirring noise can be heard as the *'s laser aligns itself, followed by a faint pulse as it fires.} {
 mrandomplay Ship\Combat\PlanetaryLaser@Ext;
 #GagLine 1 All
} {Main}
#Alias focus {
 #If {@InterruptOnFocus=1} {speech stop};
 MainTrigSwitch 0 Focus;
 #Exec {>focus%RTrim( %0)}
}
#Trigger {*: *} {
 #If {%IfWord(%1,Coordinates|Current coordinates|Current galactic coordinates,|)=1} {
  MainTrigSwitch 1
 } {
  #Var ShipCombatTmp1 {%Replace(%2,~, ,)};
  #If {%IsNumber(@ShipCombatTmp1)=1} {
   #If {@InterruptOnFocus=1} {speech stop};
   #If {%IfWord(%2,@FocusShipCoords,%CRLF)=0} {
    #Var FocusShipCoords {%2};
    MirianiSoundPlay Standard Ship\Computer\Beep@Ext 10
   };
   #Var FocusShip {$};
   #Var FocusShipName {%1};
   #Var FocusShipX {%Word(%2,~, ,1)};
   #Var FocusShipY {%Word(%2,~, ,2)};
   #Var FocusShipZ {%Word(%2,~, ,3)};
   #Var ShipCombatTmp1 {%Replace(@FocusFormatString,$,~@FocusShip)};
   #Sub {%Expand(@ShipCombatTmp1)};
   MainTrigSwitch 1
  }
 }
} {Focus}
#Trigger {That's not a ship.} {MainTrigSwitch 1 Focus} {Focus}
#Trigger {Invalid coordinates. Range: (*) to (*).} {
 MirianiSoundPlay Standard Ship\Misc\Drunk@Ext;
 #Sub {Invalid coordinates.}
} {Main|Starship}
#Trigger {You hear * as * launched.} {
 mwplay Ship\Combat\CombatDroneLaunch*8@Ext;
 #Sub {%2 launched.}
} {Main|Starship}
#Trigger {You input a command to *.} {
 MirianiSoundPlay Standard Ship\Computer\CombatDroneRecall@Ext;
 #Sub {You %1.}
} {Main|Starship}
#Trigger {You * the stun turret camera*.} {
 #If {%Pos(enable,%1)>0} {
  MirianiSoundPlay Standard General\Device\Activate@Ext
 } {
  MirianiSoundPlay Standard General\Device\Deactivate@Ext
 }
} {Main}
#Trigger {Those coordinates are too far away to establish a lock.} {
 MirianiSoundPlay Standard Ship\Combat\OutOfRange@Ext;
 #Sub {Out of range.}
} {Main|Starship}
#Trigger {The ship rocks and shakes intensely as it is thrown violently by *} {
 #Sub {Hit by %1};
 mwplay Ship\Combat\PushPulse@Ext
} {Main|Starship}
#Trigger {Those coordinates are already locked.} {
 #Sub {Already locked.}
} {Main|Starship}
#Alias dam {MainTrigSwitch 0 DamageReader;#Exec {>dam%RTrim( %0)}} {Craft}
#Alias damage {MainTrigSwitch 0 DamageReader;#Exec {>damage%RTrim( %0)}} {Craft}
#Trigger {*: *~%*} {
 #If {%Pos(~",%0)=0 and %IfWord(%Left(%0,1),>| |#|-|[|~(,|)=0 and %NumWords(%0,~%)<8} {
  #ForAll {%Replace(%0,~%,|)} {
   #If {%Defined(DamageReader)=1 and %NumWords(%i,:)=2} {
    #Var DamageReader {%Trim(%Word(%i,:,2))};
    #If {%IsNumber(@DamageReader)=1} {
     #If {@DamageReader>=80 and @DamageReader<100} {
      #If {%Pos(HULL: ,%i)>0} {
       MirianiSoundPlay Standard Ship\Combat\HullCrit@Ext -20
      } {
       MirianiSoundPlay Standard Ship\Combat\Critical@Ext -15
      };
      MainTrigSwitch 1
     }
    }
   }
  }
 }
} {Main|Craft|DamageReader}
#Trigger {*An escape pod suddenly lands nearby.} {MirianiSoundPlay Standard Ship\Misc\EscapePodDescend@Ext} {Main}
#Trigger {You hear a soft mechanical whine as the fighter bay's hatch *s.} {
 #GagLine 1 Voice;
 #If {%1="open"} {MirianiSoundPlay Standard Ship\Move\BCFighterDock@Ext};
 #If {%1="close"} {MirianiSoundPlay Standard Ship\Misc\DockingBayDoorClose@Ext}
} {Main}
#Trigger {The lights overhead suddenly flicker as they change from * to green.} {
 ThemeMusicFadeOut;
 MirianiSoundPlay Standard Ship\Combat\GreenAlert*2@Ext;
 #If {%Defined(AlertSoundHandle)=1} {#PC @AlertSoundHandle Stop;#UnVar AlertSoundHandle}
} {Main|Starship}
#Trigger {* damaged beyond repair.} {MirianiSoundPlay Standard Ship\Computer\Error@Ext} {Main|Craft}
#Trigger {* fiercely inputs commands into a firing control console.} {
 #Var SomeoneElseFires 1;
 MirianiSoundPlay Standard Ship\Computer\Keyboard*15@Ext
} {Main|Starship}
#Trigger {Praelor * has *.} {
 #Var PraelorActions {launched undocked entered docked jumped};
 #If {%IfWord(%word(%2, ,1),@PraelorActions, )=1} {
  MirianiSoundPlay {Praelor} Ship\Combat\Praelor@Ext;
  PushMessage Praelor0Activity {%0}
 }
} {Main}
#Trigger {* ha* * from Praelor *.} {
 #Sub {%1 %3 from %4.};
 MirianiSoundPlay {Praelor} Ship\Combat\Praelor@Ext;
 PushMessage Praelor0Activity {%1 %3 from %4.}
} {Main}
#Trigger {* slam* into the hull. You hear a loud grinding noise.} {
 #Sub {Hit by %1.};
 mwplay Ship\Combat\YouHit*11@Ext 20
} {Main|Starship}
#Trigger {There is a gut-wrenching clang as the ship collides with * at high velocity.} {
 mwplay Ship\Combat\Rebound*5@Ext;
 #Sub {Collition with %1.}
} {Main|Starship}
#Trigger {Combat Drone Control:} {
 #GagLine 1 Voice;
 #Class CombatDrone Enable
} {Main|Starship}
#Trigger {* Combat Drones: */*} {
 #Sub {%1: %2/%3}
} {CombatDrone}
#Trigger {Currently Targetting: *.} {
 #Sub {%1};
 #Class CombatDrone Disable
} {CombatDrone}
#Trigger {The ship rocks violently as it comes into contact with *} {
 #Sub {The ship hits %1};
 MirianiSoundPlay Standard Ship\Combat\YouHit*6@Ext
} {Main|Starship}
#Trigger {You hear a series of loud bangs as * missiles explode on the hull.} {
 MrandomPlay Ship\Combat\Projectile*3.wav;
 #GagLine 1 All
} {Main}
#Trigger {You hear a series of * as the ship fires * missiles.} {
 MirianiSoundPlay Standard Ship\Combat\Missiles@Ext;
 #GagLine 1 All
} {Main}
#Trigger {You hear a loud * sound as a disarmament pulse is fired.} {
 #Sub {Disarmament pulse fired.};
 MirianiSoundPlay Standard Ship\Combat\disarm@Ext
} {Main}
#Trigger {You hear * sound as * immobilization pulse *.} {
 MirianiSoundPlay Standard Ship\Combat\immobilize@Ext;
 #Sub {Immobilization pulse fired.}
} {Main}
#Trigger {You hear * the starship's CDP launcher.} {
 MirianiSoundPlay Standard Ship\Combat\disrupt@Ext;
 #Sub {CDP fired.}
} {Main}
#Trigger {You hear a sharp * sound as a sensor interruption pulse is fired.} {
 MirianiSoundPlay Standard Ship\Combat\interrupt@Ext;
 #Sub {Sensor interruption pulse fired.}
} {Main}
